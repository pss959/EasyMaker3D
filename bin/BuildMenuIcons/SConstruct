import subprocess

env = Environment()

env.Replace(
    INSTALLSTR = 'Installing "$SOURCE"'
)

# Use MD5 content signatures for any targets built with the attached
# construction environment. Only new ones will be installed.
env.Decider('content')

# Run MakeIcon with the 'list' option to get a list of all icons.
icons = Split(subprocess.check_output(['./MakeIcon.py', 'list'], text=True))

sources = Split('IconBuilder.py ImageCreator.py MakeIcon.py Point.py Rect.py')

# Build each one.
installs = []
for icon in icons:
    filename = icon + '.png'
    env.Command(filename, sources, './MakeIcon.py ' + icon)
    installs += env.Install('../MakerVR/Assets/Textures/Resources/MenuIcons/',
                            filename)

env.Alias('Install', installs)
env.Default('Install')
