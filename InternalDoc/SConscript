# -----------------------------------------------------------------------------
# scons configuration for internal IMakerVR documentation using Doxygen.
#
# Targets:
#    - Doc [DEFAULT]
#          Generates internal Doxygen documentation in "doxygen" directory.
# -----------------------------------------------------------------------------

Import('env')

# Determine the current version number from XXXX or import it.
version_number = '0.0.1'

doc_env = env.Clone(
    OUTPUT_DIR     = '$ABS_BUILD_DIR/InternalDoc',
    VERSION_NUMBER = version_number,
)

# Set up environment variables for Doxyfile to access.
doc_env.Append(
    ENV = {
        'VERSION_NUMBER'   : version_number,
        'OUTPUT_DIRECTORY' : doc_env.subst('$OUTPUT_DIR'),
    }
)

# Recursive function to find all files of certain types for Doxygen input.
def FindSources(root_dir, patterns):
    from fnmatch import filter as fnfilter
    from os      import walk
    from os.path import join
    sources = []
    for root, dirnames, filenames in walk(root_dir):
        for pattern in patterns:
            sources += [join(root, fn) for fn in fnfilter(filenames, pattern)]
    return sources

dox_files = FindSources('.',      ['*.dox'])
src_files = FindSources('../src', ['*.h', '*.cpp'])

doxygen_sources = ['Doxyfile'] + dox_files + src_files

doc = doc_env.Command('$OUTPUT_DIR/html/index.html', doxygen_sources,
                  ['echo === Building doc for version: $VERSION_NUMBER',
                   'doxygen InternalDoc/Doxyfile'])

# -----------------------------------------------------------------------------
# Doxygen aliases
# -----------------------------------------------------------------------------

env.Alias('Doc', doc)
