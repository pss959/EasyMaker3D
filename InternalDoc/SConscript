# -----------------------------------------------------------------------------
# scons configuration for internal documentation using Doxygen.
#
# Generates internal Doxygen documentation in the "InternalDoc/html"
# subdirectory of the build directory.
# -----------------------------------------------------------------------------

Import('doc_build_dir', 'APP_NAME', 'VERSION_STRING')

env = Environment(
    ABS_BUILD_DIR = Dir(f'#{doc_build_dir}').abspath,
    OUTPUT_DIR    = '$ABS_BUILD_DIR/InternalDoc',
    DOXYGEN       = '/local/inst/doxygen-1.9.5/bin/doxygen',
)

# Set up environment variables for Doxyfile to access.
env.Append(
    ENV = {
        'APP_NAME'         : APP_NAME,
        'VERSION_STRING'   : VERSION_STRING,
        'OUTPUT_DIRECTORY' : env.subst('$OUTPUT_DIR'),
    }
)

# Recursive function to find all files of certain types for Doxygen input.
def FindSources(root_dir, patterns):
    from fnmatch import filter as fnfilter
    from os      import walk
    from os.path import join
    sources = []
    for root, dirnames, filenames in walk(root_dir):
        for pattern in patterns:
            sources += [join(root, fn) for fn in fnfilter(filenames, pattern)]
    return sources

dox_files = FindSources('.',      ['*.dox'])
src_files = FindSources('../src', ['*.h', '*.cpp'])

doxygen_sources = ['Doxyfile'] + dox_files + src_files

doc = env.Command('$OUTPUT_DIR/html/index.html', doxygen_sources,
                  ['echo = Building internal doc for version: $VERSION_STRING',
                   '$DOXYGEN InternalDoc/Doxyfile'])
Return('doc')
