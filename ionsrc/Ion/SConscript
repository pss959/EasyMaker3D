from brief import Brief

Import('brief', 'build_dir', 'optimize', 'platform_env')

src_dir = Dir('.').srcnode().abspath

# -----------------------------------------------------------------------------
# Base environment setup.
# -----------------------------------------------------------------------------

env = platform_env.Clone()

env.Replace(
    CPPPATH   = [
        '/usr/include',
        '/usr/include/jsoncpp',  # Use JSONCPP from system.
        '.',
        'ion/port/override',
        'third_party/google',
        'third_party/absl',
        'third_party/google',
        'third_party/image_compression',
    ],
    CPPDEFINES = [
        'ARCH_K8',
        'HAVE_UNSIGNED_CHAR', 'HAVE_UNSIGNED_SHORT',  # For libjpeg_turbo.
        'OPENCTM_NO_CPP',
        ('ION_ARCH_X86_64', '1'),
        ('ION_NO_RTTI', '0'),
        ('JSON_INC', 'jsoncpp/json'),
    ],
    LIBPATH = [],
)

# Handle brief mode.
if brief:
    Brief(env)

# Creating source files from asset definition files.
zipasset_builder = Builder(
    action = f'python3 {src_dir}/ion/dev/zipasset_generator.py $SOURCE . $TARGET',
    suffix='.cc', src_suffix='.iad')
env.Append(BUILDERS = { 'ZipAsset' : zipasset_builder })

# -----------------------------------------------------------------------------
# Platform-specific environment setup.
# -----------------------------------------------------------------------------

platform = platform_env['PLATFORM']

if platform == 'windows':
    env.Append(
        CPPDEFINES = [
            'COMPILER_HAS_RVALUEREF',
            'NOGDI',                # Disables "ERROR" macro.
            'NOMINMAX',
            'PRAGMA_SUPPORTED',
            'UNICODE=1',
            'WIN32_LEAN_AND_MEAN=1',
            '_CRT_SECURE_NO_DEPRECATE',
            '_USE_MATH_DEFINES',    # Enables M_PI.
            '_WIN32',
            ('ION_API', '__declspec(dllexport)'),
            ('ION_APIENTRY', 'APIENTRY'),
            ('ION_PLATFORM_WINDOWS', '1'),
            ('OS_WINDOWS' 'OS_WINDOWS'),
        ],
        LINKFLAGS = ['-static'],   # Use static versions when possible.
        LIBS = [
            'gdi32',
            'imagehlp',
            'opengl32',
            'user32',
            'Ws2_32'
        ],
        # There is a dependency cycle between freetype2 and harfbuzz.
        # Apparently people just work around it. Since LIBS has unique names,
        # can't add anything twice there, so just add freetype again this way:
        _LIBFLAGS = ['-lfreetype'],
    )
    pkg_config_opts = '--static'
    packages = [
        'freetype2',
        'jsoncpp',
        'libjpeg',
        'libpng',
        'minizip',
        'tinyxml2',
        'zlib',
    ]

elif platform == 'linux':
    env.Append(
        CPPDEFINES = [
            ('ION_API', ''),
            ('ION_APIENTRY', ''),
            ('ION_PLATFORM_LINUX', '1'),
        ],
        LIBS = ['dl', 'pthread'],
    )
    pkg_config_opts = ''
    packages = [
        'freetype2',
        'jsoncpp',
        'libjpeg',
        'libpng',
        'minizip',
        'tinyxml2',
        'zlib',
    ]

common_flags = [
    '--std=c++14',
    '-Wall',
    '-Wno-strict-aliasing',    # Ion has issues with this.
    '-Wno-format-truncation',  # Disable warnings in snprintf() calls.
    '-Wno-unused-result',      # Disable warnings for fread() calls.
    '-Wno-array-bounds',       # Disable warning in tracinghelper.cc.
]

# Specialize for debug or optimized modes.
if optimize:
    env.Append(
        CXXFLAGS   = common_flags + ['-O3'],
        LINKFLAGS  = common_flags + ['-O3', '-Wl,--strip-all'],
        CPPDEFINES = [('CHECK_GL_ERRORS', 'false')],
    )
else:
    env.Append(
        CXXFLAGS   = common_flags + ['-g'],
        LINKFLAGS  = common_flags + ['-g'],
        CPPDEFINES = [
            '_DEBUG',
            ('DEBUG', '1'),
            ('ION_DEBUG', '1'),
        ],
    )

package_str = ' '.join(packages)
env.ParseConfig(f'pkg-config {pkg_config_opts} {package_str} --cflags --libs')

# -----------------------------------------------------------------------------
# Source files. A "name" means there is both a .h header and a .cc source file.
# -----------------------------------------------------------------------------

def MakeTuple(subdir, names=[], headers=[], sources=[], assets=[]):
    return (subdir, names, headers, sources, assets)

# Platform-specific lists:
if platform == 'linux':
    portgfx_names   = []
    portgfx_sources = [ 'glxcontext.cc' ]
elif platform == 'windows':
    portgfx_names   = [ 'window_win32' ]
    portgfx_sources = [ 'wglcontext.cc' ]

# Each entry is a tuple: (subdir, names, headers, sources).
ion_files = [
    MakeTuple(subdir='analytics',
              names=[
                  'benchmark',
                  'benchmarkutils',
                  'gpuperformance',
              ],
              headers=['discrepancy.h']),
    MakeTuple(subdir='base',
              names=[
                  'allocatable',
                  'allocationmanager',
                  'allocator',
                  'bufferbuilder',
                  'calllist',
                  'datacontainer',
                  'datetime',
                  'fullallocationtracker',
                  'invalid',
                  'logchecker',
                  'logging',
                  'memoryzipstream',
                  'notifier',
                  'readwritelock',
                  'setting',
                  'settingmanager',
                  'spinmutex',
                  'staticsafedeclare',
                  'stringtable',
                  'stringutils',
                  'threadspawner',
                  'utf8iterator',
                  'workerpool',
                  'zipassetmanager',
              ],
              headers=[
                  'allocationsizetracker.h',
                  'allocationtracker.h',
                  'argcount.h',
                  'array2.h',
                  'circularbuffer.h',
                  'enumhelper.h',
                  'functioncall.h',
                  'indexmap.h',
                  'lockguards.h',
                  'nulllogentrywriter.h',
                  'once.h',
                  'referent.h',
                  'scalarsequence.h',
                  'scopedallocation.h',
                  'serialize.h',
                  'shareable.h',
                  'sharedptr.h',
                  'signal.h',
                  'static_assert.h',
                  'threadlocalobject.h',
                  'type_structs.h',
                  'variant.h',
                  'varianttyperesolver.h',
                  'vectordatacontainer.h',
                  'weakreferent.h',
                  'zipassetmanagermacros.h',
              ]),
    MakeTuple(subdir='gfx',
              names=[
                  'attribute',
                  'attributearray',
                  'bufferobject',
                  'computeprogram',
                  'cubemaptexture',
                  'framebufferobject',
                  'graphicsmanager',
                  'image',
                  'indexbuffer',
                  'node',
                  'renderer',
                  'resourceholder',
                  'resourcemanager',
                  'sampler',
                  'shader',
                  'shaderinput',
                  'shaderinputregistry',
                  'shaderprogram',
                  'shape',
                  'statetable',
                  'texture',
                  'tracecallextractor',
                  'tracinghelper',
                  'tracingstream',
                  'uniform',
                  'uniformblock',
                  'uniformholder',
                  'updatestatetable',
              ],
              headers=[
                  'glfunctiontypes.h',
                  'graphicsmanagermacrodefs.h',
                  'graphicsmanagermacroundefs.h',
                  'openglobjects.h',
                  'transformfeedback.h',
              ],
              sources=['tracinghelperenums.cc']),
    MakeTuple(subdir='gfxprofile',
              names=['gpuprofiler']),
    MakeTuple(subdir='gfxutils',
              names=[
                  'buffertoattributebinder',
                  'frame',
                  'printer',
                  'shadermanager',
                  'shadersourcecomposer',
                  'shapeutils',
              ],
              headers=['resourcecallback.h']),
    MakeTuple(subdir='image',
              names=[
                  'conversionutils',
                  'exportjpeg',
                  'ninepatch',
                  'renderutils',
              ]),
    MakeTuple(subdir='math',
              names=[
                  'matrixutils',
                  'rotation',
                  'transformutils',
              ],
              headers=[
                  'angle.h',
                  'angleutils.h',
                  'fieldofview.h',
                  'matrix.h',
                  'range.h',
                  'rangeutils.h',
                  'utils.h',
                  'vector.h',
                  'vectorutils.h',
              ]),
    MakeTuple(subdir='port',
              names=[
                  'barrier',
                  'break',
                  'environment',
                  'fileutils',
                  'logging',
                  'memory',
                  'memorymappedfile',
                  'semaphore',
                  'stacktrace',
                  'string',
                  'threadutils',
                  'timer',
              ],
              headers=[
                  'align.h',
                  'atomic.h',
                  'macros.h',
                  'nullptr.h',
                  'static_assert.h',
                  'trace.h',
                  'useresult.h',
              ],
              sources=[
                  'logging_cerr.cc',
              ]),
    MakeTuple(subdir='portgfx',
              names=portgfx_names + [
                  'glcontext',
                  'isextensionsupported',
                  'setswapinterval'
              ],
              headers=[
                  'glenums.h',
                  'glheaders.h'
              ],
              sources=portgfx_sources,
              ),
    MakeTuple(subdir='profile',
              names=[
                  'calltracemanager',
                  'profiling',
                  'timeline',
                  'timelineevent',
                  'timelinenode',
                  'tracerecorder',
                  'vsyncprofiler',
              ],
              headers=[
                  'timelineframe.h',
                  'timelinemetric.h',
                  'timelinerange.h',
                  'timelinescope.h',
                  'timelinesearch.h',
                  'timelinethread.h',
              ]),
    MakeTuple(subdir='remote',
              names=[
                  'calltracehandler',
                  'httpserver',
                  'nodegraphhandler',
                  'remoteserver',
                  'resourcehandler',
                  'settinghandler',
                  'shaderhandler',
                  'tracinghandler',
              ],
              assets=[
                  'res/calltrace.iad',
                  'res/geturi_cc.iad',
                  'res/nodegraph.iad',
                  'res/resources.iad',
                  'res/root.iad',
                  'res/settings.iad',
                  'res/shader_editor.iad',
                  'res/tracing.iad',
              ]),
    MakeTuple(subdir='text',
              names=[
                  'basicbuilder',
                  'binpacker',
                  'builder',
                  'font',
                  'fontimage',
                  'fontmanager',
                  'freetypefont',
                  'freetypefontutils',
                  'icuutils',
                  'layout',
                  'outlinebuilder',
                  'sdfutils',
              ],
              headers=[
                  'fontmacros.h',
              ]),
]

# Special case for third_party code that is built inside Ion.
third_party_env = env.Clone(
    CPPDEFINES = [
        ('IS_LITTLE_ENDIAN', '1'),

        # For mongoose:
        'USE_WEBSOCKET',
        'NO_SSL',
        'DEBUG_TRACE',
    ],
    CPPPATH = [
        '.',
        'third_party/absl',
        'third_party/google',
        'third_party/image_compression',
        'third_party/openctm/tools/rply',
        'third_party/openctm/lib',
    ],
)
third_party_sources = [
    'lodepng/lodepng.cpp',
    'mongoose/mongoose.c',
    'omaha/omaha/base/security/b64.c',
    'openctm/tools/3ds.cpp',
    'openctm/tools/common.cpp',
    'openctm/tools/dae.cpp',
    'openctm/tools/lwo.cpp',
    'openctm/tools/mesh.cpp',
    'openctm/tools/obj.cpp',
    'openctm/tools/off.cpp',
    'openctm/tools/ply.cpp',
    'openctm/tools/rply/rply.c',
    'openctm/tools/stl.cpp',
    'unzip/unzip.c',
    'image_compression/image_compression/internal/compressor4x4_helper.cc',
    'image_compression/image_compression/internal/dxtc_compressor.cc',
    'image_compression/image_compression/internal/dxtc_const_color_table.cc',
    'image_compression/image_compression/internal/dxtc_to_etc_transcoder.cc',
    'image_compression/image_compression/internal/etc_compressor.cc',
    'image_compression/image_compression/internal/pixel4x4.cc',
    'image_compression/image_compression/internal/pvrtc_compressor.cc',
    '../util/stb_image.c',
    '../util/stb_image_write.c',
]

third_party_objects = [
    third_party_env.SharedObject(f'third_party/{source}')
    for source in third_party_sources]

# -----------------------------------------------------------------------------
# Building targets.
# -----------------------------------------------------------------------------

# Concatenate all source files.
ion_sources = []
for (subdir, names, headers, sources, assets) in ion_files:
    ion_sources += [f'ion/{subdir}/{name}.cc' for name in names]
    ion_sources += [f'ion/{subdir}/{source}' for source in sources]
    for asset in assets:
        source = f'{src_dir}/ion/{subdir}/{asset}'
        target = f'ion/{subdir}/{asset}'.replace('.iad', '.cc')
        ion_sources.append(env.ZipAsset(source=source, target=target))

lib = env.SharedLibrary(f'#{build_dir}/ionshared',
                        ion_sources + third_party_objects)

Return('lib')
