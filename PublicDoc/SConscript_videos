# -----------------------------------------------------------------------------
# Generates videos for public documentation using the capturevideo application.
# -----------------------------------------------------------------------------

from os      import environ
from os.path import join

Import('capturevideo')

# -----------------------------------------------------------------------------
# Environment.
# -----------------------------------------------------------------------------

env = Environment(
    # Copy the environment variables for the capturevideo application.
    ENV = environ.copy(),
)

# -----------------------------------------------------------------------------
# Snapshot Video generation and annotation.
# -----------------------------------------------------------------------------

root_dir   = env.Dir('#').abspath   # Work around SCons issue.
video_dir  = './docs/extra/videos'
script_dir = './videos/scripts'

# List of capturevideo script files.
scripts = [
    'Doorstop',
    'GeneralTools',
    'SpecializedTools',
    'WorkArea',
]

def VideoFiles(name):
    return [join(video_dir, name + '.mp4'),
            join(video_dir, name + '.vtt')]

# Video script processing.
videos = []
for script in scripts:
    script_file  = script + '.econf'
    video_format = 'yuvmp4'
    cap_args = f'--format={video_format} --maximize --offscreen'
    video = env.Command(VideoFiles(script),
                        [capturevideo, join(script_dir, script_file)],
                        [Mkdir(video_dir),
                         f'{capturevideo.abspath} {cap_args} {script_file}'],
                        chdir=root_dir)
    # This prevents capturevideo from running in parallel.
    # env.SideEffect('dummy', video) # XXXX
    videos.append(video)

env.Alias('Videos', videos)

Return('videos')
