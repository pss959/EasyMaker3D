Import('env')

tests = [
    'EventTest',
    'FlagsTest',
    'HandTest',
    'ParserTest',
]

test_env = env.Clone()

test_env.Append(
    CPPPATH = ['#submodules/googletest/googletest/include'],
    LIBPATH = ['$ABS_BUILD_DIR', '$ABS_BUILD_DIR/googletest'],
    LIBS    = ['imakervr', 'gtest_main', 'gtest', 'pthread'],
    RPATH   = '$LIBPATH',
)

# Builds all unit test into a single program.
unit_test = test_env.Program(f'#$BUILD_DIR/tests/UnitTest',
                             [f'#$BUILD_DIR/tests/{test}.cpp'
                              for test in tests])

env.Alias('Tests', unit_test)

test_args = ''
env.Alias('Runtests', unit_test, f'$SOURCE {test_args}')

# Make sure Runtests is always considered out of date.
env.AlwaysBuild('Runtests')
