Import('app_dict', 'app_env')

main_app_name = app_dict['APP_NAME']

# Build the applications. The doc-related apps build only on Linux.
app_names = [main_app_name, 'nodeviewer', 'printtypes']
if app_env['PLATFORM'] == 'linux':
    app_names += ['capturevideo', 'createcheatsheet', 'snapimage']

apps_extra_sources = [
    '../App/Args.cpp',
]

apps_extra_objects = [app_env.SharedObject(src) for src in apps_extra_sources]

# Return a dictionary mapping app name to executable.
apps = {}
for app_name in app_names:
    app_object = app_env.SharedObject(f'{app_name}.cpp')
    app = app_env.Program(app_name, [app_object] + apps_extra_objects)
    apps[app_name] = app

Return('apps')
