Import('app_dict', 'app_env')

main_app_name = app_dict['APP_NAME']

# -----------------------------------------------------------------------------
# Environments.
# -----------------------------------------------------------------------------

# Environment for an app that needs scripting (Linux only).
if app_env['PLATFORM'] == 'linux':
    script_env = app_env.Clone()
    script_env.Append(LIBS = '$SCRIPT_LIB_NAME')
    script_env.AddPackages(script_env['VIDEO_PACKAGES'])

# -----------------------------------------------------------------------------
# Building applications.
# -----------------------------------------------------------------------------

# Return a dictionary mapping app name to executable.
apps = {}

# Builds an app and adds it to the apps dictionary.
def BuildApp_(name, env):
    app_object = env.SharedObject(f'{name}.cpp')
    apps[name] = env.Program(name, [app_object])

# Build the main applications.
for name in [main_app_name, 'nodeviewer', 'printtypes']:
    BuildApp_(name, app_env)

# The doc-related apps build only on Linux. Some require different libraries.
if app_env['PLATFORM'] == 'linux':
    BuildApp_('createcheatsheet', app_env)
    BuildApp_('snapimage',        script_env)
    BuildApp_('capturevideo',     script_env)

Return('apps')
